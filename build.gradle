apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'maven-publish'

ext {
    // Minimum required Testerra version
    testerraCompileVersion = '2.0-RC-2'
    // Unit tests use the latest Testerra version
    testerraTestVersion = '[2.0-RC,3-SNAPSHOT)'

    // Default version
    moduleVersion = '2-SNAPSHOT'
    if (System.properties.containsKey('moduleVersion')) {
        moduleVersion = System.getProperty('moduleVersion')
    }

    group 'eu.tsystems.mms.tic.testerra'
    version moduleVersion
}

// Do not move this integration because `group` and `version` is needed for publishing
apply from: rootProject.file('publish.gradle')

dependencies {
    compileOnly 'eu.tsystems.mms.tic.testerra:core:' + testerraCompileVersion
    implementation 'eu.tsystems.mms.tic.testerra:surefire-connector:' + testerraCompileVersion

    implementation 'org.reflections:reflections:0.9.12'
    compile "com.google.guava:guava:25.1-jre"

    // https://mvnrepository.com/artifact/org.glassfish.jersey.core/jersey-client
    implementation group: 'org.glassfish.jersey.core', name: 'jersey-client', version: '2.31'

    implementation 'javax.ws.rs:javax.ws.rs-api:2.0.1'
    implementation 'com.sun.jersey.contribs:jersey-multipart:1.19.3'
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: '2.3.1'
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.3.1'

    testImplementation 'eu.tsystems.mms.tic.testerra:core:' + testerraTestVersion
    testImplementation 'eu.tsystems.mms.tic.testerra:surefire-connector:' + testerraTestVersion
    testImplementation 'commons-codec:commons-codec:1.13'
}

test {
    useTestNG()
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

task unitTests(type: Test) {
    useTestNG() {
        suites 'src/test/resources/unit_tests.xml'
    }
}

task integrationTests(type: Test) {
    useTestNG() {
        suites 'src/test/resources/integration_tests.xml'
    }
}
